# Copyright (c) 2023 The Brave Authors. All rights reserved.
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this file,
# You can obtain one at https://mozilla.org/MPL/2.0/.

import("//base/win/embedded_i18n/generate_embedded_i18n.gni")
import("//brave/components/brave_vpn/common/buildflags/buildflags.gni")

assert(enable_brave_vpn)

source_set("resources") {
  inputs = [
    "resource.h",
    "resources_en.rc",
  ]
  deps = [ ":strings" ]
}

generate_embedded_i18n("generate_strings") {
  visibility = [ ":strings" ]
  extractor_datafile = "//brave/components/brave_vpn/browser/connection/wireguard/win/brave_vpn_wireguard_service/status_tray/resources/create_wireguard_string_rc.py"
  grdfile_folder = "//brave/components/resources"

  xtb_relative_path = "strings"
  grdfile_name = "brave_components_strings"

  # Exclude languages not supported by Brave
  # https://github.com/brave/brave-browser/wiki/Strings-and-Localization#information-for-submitting-localization-orders
  brave_supported_i18_locales = default_embedded_i18_locales - [
                                  "af",
                                  "as",
                                  "az",
                                  "be",
                                  "bs",
                                  "cy",
                                  "eu",
                                  "hy",
                                  "is",
                                  "ka",
                                  "kk",
                                  "km",
                                  "ky",
                                  "lo",
                                  "mk",
                                  "mn",
                                  "my",
                                  "ne",
                                  "pa",
                                  "or",
                                  "si",
                                  "sq",
                                  "sr-Latn",
                                  "ur",
                                  "uz",
                                  "zh-HK",
                                  "zu",
                                ]
  grd_files_info = [ [
        grdfile_folder,
        grdfile_name,
        xtb_relative_path,
        brave_supported_i18_locales,
      ] ]
  sources = [ "//brave/components/resources/brave_components_strings.grd" ]
  output_file_name_base = "wireguard_strings"
}

source_set("strings") {
  sources = get_target_outputs(":generate_strings")
  public_deps = [ ":generate_strings" ]
}
